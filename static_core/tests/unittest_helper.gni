# Copyright (c) 2025-2026 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/runtime_core/static_core/ark_config.gni")
import("//build/test.gni")

# Author: Petrov Igor (@ipetrov)
# Brief: The common template for unit tests in static runtime
# Arguments:
#  Required:
#   sources - list of compiling test source files
#   module_out_path - target and logs output path.
#                     The target will be placed in ${root_out_dir}/tests/unittest/${module_out_path}
#  Optional:
#   configs, deps, externals_deps, etc. - like in ohos_executable
#   env - list of environment variable strings in "VAR=VALUE" format
#         Example: env = [ "PANDA_STD_LIB=/path/to/stdlib.abc", "ARK_BOOTCLASSPATH=/path/to/file.abc" ]
#   ld_lib_paths - list of additional load library paths. The paths will be added to LD_LIBRARY_PATH as
#                  LD_LIBRARY_PATH=${root_out_dir}/${ld_lib_path_1}:${root_out_dir}/${ld_lib_path_2}:...
template("ark_unittest") {
  ohos_unittest("${target_name}") {
    forward_variables_from(invoker,
                           "*",
                           [
                             "env",
                             "ld_lib_paths",
                             "no_cores",
                             "no_warning_filter",
                           ])
  }
  if (!ark_static_standalone_build) {
    _target_name = "${target_name}"
    _output_path = invoker.module_out_path
    _ld_lib_paths = []
    _env = []
    _no_cores = false
    _no_warning_filter = false
    if (defined(invoker.ld_lib_paths)) {
      _ld_lib_paths += invoker.ld_lib_paths
    }
    if (defined(invoker.env)) {
      _env = invoker.env
    }
    if (defined(invoker.no_cores)) {
      _no_cores = invoker.no_cores
    }
    if (defined(invoker.no_warning_filter)) {
      _no_warning_filter = invoker.no_warning_filter
    }

    action("${target_name}_gtests") {
      _host_target_name = ":${_target_name}(${host_toolchain})"
      _root_out_dir = get_label_info(_host_target_name, "root_out_dir")

      testonly = true

      # NOTE(ipetrov, 8327(gitcode)): Need to support:
      #  * custom TSAN options
      #  * (?) rapid check
      script = "//arkcompiler/runtime_core/static_core/tests/run_executable.sh"

      deps = [ _host_target_name ]

      _libs_list_str = rebase_path(_root_out_dir) + "/arkcompiler/runtime_core"
      foreach(lib_path, _ld_lib_paths) {
        _libs_list_str += ":" + rebase_path(_root_out_dir) + "/" + lib_path
      }

      _test_work_dir =
          rebase_path(_root_out_dir) + "/tests/unittest/${_output_path}"

      args = [
        "--exec-file",
        _test_work_dir + "/${_target_name}",
        "--output-file",
        _test_work_dir + "/${_target_name}_output.txt",
        "--libs-path",
        _libs_list_str,
      ]

      foreach(env_var, _env) {
        args += [
          "--env",
          env_var,
        ]
      }
      if (_no_cores) {
        args += [ "--no-cores" ]
      }
      if (_no_warning_filter) {
        args += [ "--no-warning-filter" ]
      }
      args += [
        "--gtest_brief=0",
        "--gtest_death_test_style=threadsafe",
        "--gtest_shuffle",
      ]

      inputs =
          [ "$_root_out_dir/tests/unittest/${_output_path}/${_target_name}" ]
      outputs = [ "$target_out_dir/${_target_name}/" ]
    }
  } else {
    # Dummy implementation for ark_static_standalone_build
    group("${target_name}_gtests") {
    }
  }
}

template("ark_unittest_group") {
  # Source code dependencies
  group("${target_name}") {
    testonly = true
    deps = invoker.deps
  }

  # Run (action) tests dependencies
  group("${target_name}_gtests") {
    testonly = true
    deps = []
    foreach(test_dep, invoker.deps) {
      deps += [ test_dep + "_gtests" ]
    }
  }
}
