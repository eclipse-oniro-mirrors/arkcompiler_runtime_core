# Copyright (c) 2021-2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/runtime_core/static_core/ark_config.gni")
import("//arkcompiler/runtime_core/static_vm_config.gni")

if (ark_standalone_build) {
  import("$build_root/ark.gni")
} else {
  import("//build/ohos.gni")
}

ohos_static_library("libarktsbase_package") {
  deps = []
  if (enable_static_vm) {
    deps += [ ":libarktsbase_frontend_static" ]
  }

  part_name = ark_part_name
  subsystem_name = "$ark_subsystem_name"
}

ark_gen_file("events_gen") {
  template_file = "events/events_gen.h.erb"
  data = [ "events/events.yaml" ]
  api = [ "events/events.rb" ]
  output_file = "$target_gen_dir/generated/events_gen.h"
}

ark_gen_file("gen_logger_options_h") {
  template_file = "../templates/options/options.h.erb"
  data = [ "utils/logger_options.yaml" ]
  api = [ "../templates/common.rb" ]
  output_file = "$target_gen_dir/panda_gen_options/generated/logger_options.h"
}

action("generate_version_file") {
  script = "$ark_root/gn/build/make_version_file.sh"
  sources = [ "$ark_root/libarkbase/templates/ark_version.h.in" ]
  outputs = [ "$target_gen_dir/generated/ark_version.h" ]
  args = [
    rebase_path("${ark_root}", "$root_build_dir"),
    rebase_path(sources[0], "$root_build_dir"),
    rebase_path(outputs[0], "$root_build_dir"),
  ]
}

libarkbase_sources = [
  "$ark_root/libarkbase/mem/arena_allocator.cpp",
  "$ark_root/libarkbase/mem/base_mem_stats.cpp",
  "$ark_root/libarkbase/mem/code_allocator.cpp",
  "$ark_root/libarkbase/mem/mem_config.cpp",
  "$ark_root/libarkbase/mem/pool_manager.cpp",
  "$ark_root/libarkbase/mem/pool_map.cpp",
  "$ark_root/libarkbase/os/dfx_option.cpp",
  "$ark_root/libarkbase/os/filesystem.cpp",
  "$ark_root/libarkbase/os/native_stack.cpp",
  "$ark_root/libarkbase/os/time.cpp",
  "$ark_root/libarkbase/taskmanager/task.cpp",
  "$ark_root/libarkbase/taskmanager/task_manager.cpp",
  "$ark_root/libarkbase/taskmanager/task_queue_set.cpp",
  "$ark_root/libarkbase/taskmanager/task_scheduler.cpp",
  "$ark_root/libarkbase/taskmanager/timer_thread.cpp",
  "$ark_root/libarkbase/taskmanager/utils/task_time_stats.cpp",
  "$ark_root/libarkbase/taskmanager/worker_thread.cpp",
  "$ark_root/libarkbase/utils/dfx.cpp",
  "$ark_root/libarkbase/utils/json_builder.cpp",
  "$ark_root/libarkbase/utils/json_parser.cpp",
  "$ark_root/libarkbase/utils/logger.cpp",
  "$ark_root/libarkbase/utils/time.cpp",
  "$ark_root/libarkbase/utils/type_converter.cpp",
  "$ark_root/libarkbase/utils/utf.cpp",
  "$ark_root/libarkbase/utils/utils.cpp",
]

if (is_mingw) {
  libarkbase_sources += [
    "$ark_root/libarkbase/os/mutex.cpp",
    "$ark_root/platforms/windows/libarkbase/cpu_affinity.cpp",
    "$ark_root/platforms/windows/libarkbase/error.cpp",
    "$ark_root/platforms/windows/libarkbase/file.cpp",
    "$ark_root/platforms/windows/libarkbase/filesystem.cpp",
    "$ark_root/platforms/windows/libarkbase/kill.cpp",
    "$ark_root/platforms/windows/libarkbase/library_loader.cpp",
    "$ark_root/platforms/windows/libarkbase/mem.cpp",
    "$ark_root/platforms/windows/libarkbase/mem_hooks.cpp",
    "$ark_root/platforms/windows/libarkbase/system_environment.cpp",
    "$ark_root/platforms/windows/libarkbase/thread.cpp",
    "$ark_root/platforms/windows/libarkbase/trace.cpp",
  ]
} else {
  libarkbase_sources += [
    "$ark_root/libarkbase/trace/trace.cpp",
    "$ark_root/libarkbase/utils/terminate.cpp",
    "$ark_root/platforms/unix/libarkbase/cpu_affinity.cpp",
    "$ark_root/platforms/unix/libarkbase/error.cpp",
    "$ark_root/platforms/unix/libarkbase/file.cpp",
    "$ark_root/platforms/unix/libarkbase/filesystem.cpp",
    "$ark_root/platforms/unix/libarkbase/kill.cpp",
    "$ark_root/platforms/unix/libarkbase/library_loader_resolve_symbol.cpp",
    "$ark_root/platforms/unix/libarkbase/mem.cpp",
    "$ark_root/platforms/unix/libarkbase/native_stack.cpp",
    "$ark_root/platforms/unix/libarkbase/property.cpp",
    "$ark_root/platforms/unix/libarkbase/system_environment.cpp",
    "$ark_root/platforms/unix/libarkbase/thread.cpp",
    "$ark_root/platforms/unix/libarkbase/trace.cpp",
  ]

  if (is_mob) {
    libarkbase_sources +=
        [ "$ark_root/platforms/mobile/libarkbase/library_loader_load.cpp" ]
  } else {
    libarkbase_sources +=
        [ "$ark_root/platforms/unix/libarkbase/library_loader_load.cpp" ]
  }

  if (!is_mac) {
    libarkbase_sources += [
      "$ark_root/platforms/unix/libarkbase/exec.cpp",
      "$ark_root/platforms/unix/libarkbase/mem_hooks.cpp",
      "$ark_root/platforms/unix/libarkbase/pipe.cpp",
      "$ark_root/platforms/unix/libarkbase/sighook.cpp",
    ]
    if (is_asan) {
      # Futexes are not supported with ASAN
      libarkbase_sources += [ "$ark_root/libarkbase/os/mutex.cpp" ]
    } else {
      libarkbase_sources += [
        "$ark_root/platforms/unix/libarkbase/futex/fmutex.cpp",
        "$ark_root/platforms/unix/libarkbase/futex/mutex.cpp",
      ]
    }
  } else {
    libarkbase_sources += [ "$ark_root/libarkbase/os/mutex.cpp" ]
  }
}
libarkbase_libs = []
if (is_linux && ark_static_standalone_build) {
  libarkbase_sources += [
    "os/debug_info.cpp",
    "os/stacktrace.cpp",
  ]
  libarkbase_libs += [
    "dwarf",
    "z",
  ]
} else {
  libarkbase_sources += [ "os/stacktrace_stub.cpp" ]
}
if (is_debug) {
  libarkbase_sources += [ "$ark_root/libarkbase/utils/debug.cpp" ]
}

if (current_cpu == "arm64" && !is_mac && target_os != "ios") {
  libarkbase_sources += [ "$ark_root/libarkbase/arch/aarch64/cpu_features.cpp" ]
} else {
  libarkbase_sources += [ "$ark_root/libarkbase/arch/default/cpu_features.cpp" ]
}

if (is_mob) {
  copy("coherency_line_size_h") {
    sources = [ "$ark_root/platforms/mobile/libarkbase/coherency_line_size.h" ]
    outputs = [ "$target_gen_dir/generated/coherency_line_size.h" ]
  }
} else {
  copy("coherency_line_size_h") {
    sources = [ "$ark_root/platforms/common/libarkbase/coherency_line_size.h" ]
    outputs = [ "$target_gen_dir/generated/coherency_line_size.h" ]
  }
}

config("arkbase_public_config") {
  if (defined(ohos_indep_compiler_enable) && ohos_indep_compiler_enable) {
    configs = []
  } else {
    configs = [ sdk_libc_secshared_config ]
  }

  include_dirs = [
    "$ark_root_gen_dir/libarkbase",
    "$ark_root/libarkbase",
    "$target_gen_dir/generated",
    "$target_gen_dir/include",
    "$target_gen_dir",
  ]

  include_dirs += platform_include_dirs

  libs = libarkbase_libs
  if (ark_static_standalone_build) {
    ldflags = [ "-ldl" ]
  }

  #  configs = [ sdk_libc_secshared_config ]
}

if (!ark_static_standalone_build) {
  ohos_shared_headers("libpandabase_headers") {
    include_dirs = [ "$target_gen_dir/.." ]
    include_dirs += platform_include_dirs
    part_name = ark_part_name
    subsystem_name = "$ark_subsystem_name"
  }
}

libarkbase_configs = [
  ":arkbase_public_config",
  "$ark_root:ark_config",
]

group("arktsbase_header_deps") {
  deps = [
    ":coherency_line_size_h",
    ":events_gen",
    ":gen_logger_options_h",
    ":generate_version_file",
    ":logger_enum_gen_h",
    ":logger_impl_gen_h",
    ":plugins_regmasks_gen",
    ":source_language_gen",
  ]
}

ohos_source_set("libarktsbase_static") {
  sources = libarkbase_sources

  cflags_cc = [
    "-fvisibility=hidden",
    "-fvisibility-inlines-hidden",
  ]

  public_configs = libarkbase_configs

  deps = [ ":arktsbase_header_deps" ]

  if (ark_static_standalone_build) {
    deps += [ sdk_libc_secshared_dep ]
  }

  external_deps = [ sdk_libc_secshared_dep ]

  if (is_ohos && is_standard_system &&
      (current_cpu == "arm" || current_cpu == "arm64") &&
      runtime_core_enable_ffrt) {
    external_deps += [ "ffrt:libffrt" ]
    defines = [ "PANDA_USE_FFRT" ]
  }

  part_name = ark_part_name
  subsystem_name = "$ark_subsystem_name"
}

ohos_shared_library("libarktsbase") {
  deps = [ ":libarktsbase_static" ]
  if (is_mingw || is_win) {
    output_extension = "dll"
  } else {
    output_extension = "so"
  }
  if (is_gcov_tools) {
    cflags = coverage_cflags
    ldflags = coverage_ldflags
  }
  part_name = "$ark_part_name"
  subsystem_name = "$ark_subsystem_name"
}

ohos_source_set("libarktsbase_frontend_set_static") {
  sources = libarkbase_sources

  public_configs = libarkbase_configs

  deps = [ ":arktsbase_header_deps" ]

  external_deps = [ sdk_libc_secshared_dep ]

  part_name = ark_part_name
  subsystem_name = "$ark_subsystem_name"
}

ohos_source_set("libarktsbase_frontend_static") {
  deps = [ ":libarktsbase_frontend_set_static" ]
  part_name = ark_part_name
  subsystem_name = ark_subsystem_name
}

action("logger_yaml_gen") {
  script = "$ark_root/libarkbase/templates/logger_gen.rb"
  inputs = [ "$ark_root/libarkbase/templates/logger.yaml" ]
  outputs = [ "$ark_root_gen_dir/libarkbase/logger.yaml" ]
  args = [
    "-p",
    rebase_path(ark_plugin_options_yaml, root_build_dir),
    "-d",
    rebase_path(inputs[0], root_build_dir),
    "-o",
    rebase_path(outputs[0], root_build_dir),
  ]
  deps = [ "$ark_root:concat_plugins_yamls" ]
}

ark_gen_file("logger_enum_gen_h") {
  extra_dependencies = [ ":logger_yaml_gen" ]
  template_file = "templates/logger_enum_gen.h.erb"
  data = [ "$ark_root_gen_dir/libarkbase/logger.yaml" ]
  api = [ "$ark_root/libarkbase/templates/logger.rb" ]
  output_file = "$target_gen_dir/generated/logger_enum_gen.h"
}

ark_gen_file("logger_impl_gen_h") {
  extra_dependencies = [ ":logger_yaml_gen" ]
  template_file = "templates/logger_impl_gen.inc.erb"
  data = [ "$ark_root_gen_dir/libarkbase/logger.yaml" ]
  api = [ "$ark_root/libarkbase/templates/logger.rb" ]
  output_file = "$target_gen_dir/generated/logger_impl_gen.inc"
}

ark_gen_file("source_language_gen") {
  extra_dependencies = [ "$ark_root:concat_plugins_yamls" ]
  template_file = "templates/source_language.h.erb"
  data = [ ark_plugin_options_yaml ]
  api = [ "$ark_root/templates/plugin_options.rb" ]
  output_file = "$target_gen_dir/generated/source_language.h"
}

ark_gen_file("plugins_regmasks_gen") {
  extra_dependencies = [ "$ark_root:concat_plugins_yamls" ]
  template_file = "templates/plugins_regmasks.inl.erb"
  data = [ ark_plugin_options_yaml ]
  api = [ "$ark_root/templates/plugin_options.rb" ]
  output_file = "$target_gen_dir/generated/plugins_regmasks.inl"
}
