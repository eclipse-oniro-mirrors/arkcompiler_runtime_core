# Copyright (c) 2025 Huawei Device Co., Ltd.
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
# http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

import("//arkcompiler/ets_frontend/ets2panda/ets2abc_config.gni")
import("//arkcompiler/runtime_core/ark_config.gni")
import("$ark_root/tests/test_helper.gni")

test_ets_path = "$ark_root/ark_guard/abckit_wrapper/tests/"

test_ets_files = [
  "ut/module_static",
]

foreach(file, test_ets_files) {
  ets2abc_gen_abc("gen_${file}_abc") {
    test_ets = "${test_ets_path}${file}.ets"
    test_abc = "$target_out_dir/${file}.abc"

    # Only targets in this file can depend on this.
    extra_visibility = [ ":*" ]
    src_ets = rebase_path(test_ets)
    dst_file = rebase_path(test_abc)

    in_puts = [ test_ets ]
    out_puts = [ test_abc ]
  }
}

host_unittest_action("AbckitWrapperUnitTest") {
  module_out_path = "$ark_root"

  sources = [
    "ut/file_view_test.cpp",
    "ut/visitor_test.cpp",
  ]

  include_dirs = [
    "$ark_root/libabckit/include",
    "$ark_root/ark_guard/abckit_wrapper/tests/util/include",
    "$ark_root/static_core",
  ]

  configs = [
    "$ark_root/libabckit:abckit_config",
    "$ark_root/static_core/libpandabase:arkbase_public_config",
    "$ark_root/ark_guard/abckit_wrapper:abckit_wrapper_public_config",
  ]

  deps = [ "$ark_root/ark_guard/abckit_wrapper:abckit_wrapper_static" ]

  foreach(file, test_ets_files) {
    deps += [ ":gen_${file}_abc" ]
  }

  if (is_ohos && is_standard_system) {
    test_abc_dir = "/data/test"
  } else {
    test_abc_dir = rebase_path(target_out_dir)
  }

  defines = [ "ABCKIT_WRAPPER_ABC_FILE_DIR=\"${test_abc_dir}/\"" ]
}

group("host_unittest") {
  testonly = true
  deps = [ ":AbckitWrapperUnitTestAction" ]
}
